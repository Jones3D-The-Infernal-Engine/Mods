# Pulses a light Thing on and off
#
# user0: starts light pulse fx
# user1: stops light pulse fx
#
# [Crt Vavros]
# =======================================

symbols
    message timer
    message entered
    message exited
    message user0
    message user1

    sector startPulse mask=0x400 # only player will send messages
    sector stopPulse  mask=0x400

    thing lightObj nolink

    float repeatRate=0.5  desc=time_till_repeat
    float duration=0.5    desc=time_light_stays_on
    float changeTime=0.01 desc=time_to_complete_change

    float redMin=1.3
    float greenMin=1.3
    float blueMin=1.3

    float redMax=10.0
    float greenMax=10.0
    float blueMax=10.0

    float radiusMin=1.0
    float radiusMax=2.0

    # local vars
    int bPulsing=0 local
    vector color   local

    # subroutines
    float StartLightFx=0 local
    float StopLightFx=0 local
end

code
    timer:
        if (!bPulsing) return; # sanity check

        color = VectorSet(redMin, greenMin, blueMin);
        SetThingLight(lightObj, color, radiusMin, changeTime);

        Sleep(repeatRate);
        if (!bPulsing) return;

        color = VectorSet(redMax, greenMax, blueMax);
        SetThingLight(lightObj, color, radiusMax, changeTime);

        SetTimer(duration);
    return;

    user0:
        Call StartLightFx;
    return;

    user1:
        Call StopLightFx;
    return;

    entered:
        if (GetSenderRef() == startPulse) {
            Call StartLightFx;
        }
    return;

    exited:
        if (GetSenderRef() == stopPulse) {
           Call StopLightFx;
        }
    return;

    StartLightFx:
        if (!bPulsing)
        {
            color = VectorSet(redMax, greenMax, blueMax);
            SetThingLight(lightObj, color, radiusMax, changeTime);
            bPulsing = 1;
            SetTimer(duration);
        }
    return;

    StopLightFx:
        if (bPulsing)
        {
            bPulsing = 0;
            SetTimer(0);

            SetThingLight(lightObj, '0 0 0', 0, 0);
            Sleep(0.1);
            SetThingLight(lightObj, '0 0 0', 0, 0); # this make sure the light is off in case pulse was in progress
        }
    return;
end
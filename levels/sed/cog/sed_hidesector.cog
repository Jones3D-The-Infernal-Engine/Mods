# Jones 3D Cog Script
# Hides sector when indy enters specific sector(s).
#======================================================================================

symbols
    message startup
    message entered

    sector hideSector
    sector triggerHideSec1    linkid=1
    sector triggerHideSec2    linkid=1
    sector triggerHideSec3    linkid=1
    sector triggerHideSec4    linkid=1

    sector triggerShowSec1    linkid=2
    sector triggerShowSec2    linkid=2
    sector triggerShowSec3    linkid=2
    sector triggerShowSec4    linkid=2

    # local vars
    thing player     local
    surface surf     local
    int numSurfs=0   local
    int surfIdx=0    local
    int bHidden=0    local
end

code
    startup:
        player = GetLocalPlayerThing();
        numSurfs = GetNumSectorSurfaces(hideSector);
    return;

    entered:
        if (GetSourceRef() == player)
        {
            if (bHidden == 0 && GetSenderID() == 1)
            {
                #DebugPrint("Hiding sector");

                for(surfIdx = 0; surfIdx < numSurfs; surfIdx = surfIdx + 1)
                {
                    surf = GetSectorSurfaceRef(hideSector, surfIdx);
                    if (GetSurfaceAdjoin(surf) != -1)
                    {
                        ClearAdjoinFlags(surf, 0x1);
                        SetFaceGeoMode(surf, 4);
                    }
                }

                bHidden = 1;
            }
            else if (bHidden == 1 && GetSenderID() == 2)
            {
                #DebugPrint("Showing sector");
                for(surfIdx = 0; surfIdx < numSurfs; surfIdx = surfIdx + 1)
                {
                    surf = GetSectorSurfaceRef(hideSector, surfIdx);
                    if (GetSurfaceAdjoin(surf) != -1)
                    {
                        SetAdjoinFlags(surf, 0x1);
                        SetFaceGeoMode(surf, 0);
                    }
                }
                bHidden = 0;
            }
        }
    return;
end
